use "jerimum"

#Imagens
pasta = "imagens"
tabuleiro_img = Imagem("{pasta}/Tabuleiro_8x8.jpg")
rainha_branca = Imagem("{pasta}/Rainha_branca.png")
marcador_img = Imagem("{pasta}/Marcador.png")
marcador_erro_img = Imagem("{pasta}/Marcador_erro.png")
potigol_img = Imagem("{pasta}/potigol.png")

#Fonte
fonte12, fonte16 = Fonte(12), Fonte(16)

#distancias em x e y
d_x, d_y = 54

tamanho = 8

tipo Ponto
  x, y: Inteiro
  valido() = x >= 1 e x <= tamanho e y >= 1 e y <= tamanho
fim

var marcador = Ponto(20, 20)
var peças = Lista(0, Ponto(0, 0))

ocupado(casa: Ponto)
  var resultado = falso
  para c em peças faça
    se c.x == casa.x e c.y == casa.y então
      resultado := verdadeiro
    fim
  fim
  retorne resultado
fim

colide(casa: Ponto)
  var resultado = falso
  para c em peças faça
    se c <> casa e (
       c.x == casa.x                 ou    # testa a vertical
       c.y == casa.y                 ou    # testa a horizontal
       c.x + c.y == casa.x + casa.y  ou    # testa a diagonal direita
       c.x - c.y == casa.x - casa.y) então # testa a diagonal esquerda
      resultado := verdadeiro
    fim
  fim
  retorne resultado
fim

atualize()
  i = (Mouse.x + 30) div d_x
  j = (Mouse.y + 30) div d_y
  casa = Ponto(i, j)
  se casa.valido então
    marcador := Ponto(casa.x * d_x - 28, casa.y * d_y - 28)

    #adiciona rainha
    se Mouse.BOTAO_ESQUERDO e não ocupado(casa) e peças.tamanho < tamanho então
      peças := casa :: peças
    fim

    #remove rainha
    se Mouse.BOTAO_DIREITO e ocupado(casa) então
      peças := peças.remova(peças.posição(casa))
    fim
  fim
fim

desenhe()   #código para desenhar o jogo
  potigol_img.desenhe(484, 6, 1, 0, 0.4, 0.4)
  fonte16.desenhe("POTIGOL GAMES", 492, 160, 0, Cor.PRETO)

  #desenhando o tabuleiro
  tabuleiro_img.desenhe(0, 0, 0, 0, 0.3, 0.3)
  para c em peças faça
    se peças.tamanho >= tamanho e colide(c) então
    	marcador_erro_img.desenhe(d_x * c.x - 30, d_y * c.y - 30, 1, 0, 0.08, 0.08)
    fim

    #desenha rainha
    rainha_branca.desenhe(d_x * c.x - 30, d_y * c.y - 30, 2, 0, 0.08, 0.08)
  fim

  #peças
  fonte12.desenhe("Rainhas Disponiveis", 500, 190, 0, Cor.PRETO)
  para p de peças.tamanho até tamanho - 1 faça
    i, j = p div 4, p mod 4
    rainha_branca.desenhe(528 + d_x / 2 * j - 30, 243 + d_y * i - 30, 1, 0, 0.08, 0.08)
  fim

  marcador_img.desenhe(marcador.x, marcador.y, 1, 0, 0.08, 0.08)
fim

jogo.iniciar("OITO RAINHAS", 640, 480, atualize, desenhe)
