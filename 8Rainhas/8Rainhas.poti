use "jerimum"

#Imagens
pasta = "imagens"
tabuleiro_8x8 = Imagem("{pasta}/Tabuleiro_8x8.jpg")
rainha_branca = Imagem("{pasta}/Rainha_branca.png")
marcador_img = Imagem("{pasta}/Marcador.png")
potigol_img = Imagem("{pasta}/potigol.png")

#Fonte
fonte16 = Fonte(16)
fonte12 = Fonte(12)

#distancias em x e y
d_x, d_y = 54

tipo Ponto
  var x, y: Inteiro
fim

marcador = Ponto(24, 24)
casa = Ponto(1, 1)
var pecas_originais := 8

ocupado = Matriz.mutavel(8, 8, falso)

botao(x, y, h, l: Real) = Mouse.BOTAO_ESQUERDO e
  Mouse.x > x e Mouse.x < x + l e
  Mouse.y > y e Mouse.y < y + h

atualize()
  i = (Mouse.x + 30) div d_x
  j = (Mouse.y + 30) div d_y
  se i >= 1 e i <= 8 e j >= 1 e j <= 8 então
    casa.x := i
    marcador.x := casa.x * d_x - 28
    casa.y := j
    marcador.y := casa.y * d_y - 28

    #se o botão esquerdo for e casa estiver livre clicado adiciona peça
    se Mouse.BOTAO_ESQUERDO e não ocupado[i][j] e pecas_originais > 0 então
      escreva "Casa[{casa.x}, {casa.y}]"
      #adiciona rainha
      ocupado[i][j] := verdadeiro
      #escreva matriz[i][j]
      pecas_originais := pecas_originais - 1
    fim

    #se o botão esquerdo for e casa estiver livre clicado adiciona peça
    se Mouse.BOTAO_DIREITO e ocupado[i][j] então
      escreva "Casa[{casa.x}, {casa.y}]"
      #adiciona rainha
      ocupado[i][j] := falso
      #escreva matriz[i][j]
      pecas_originais := pecas_originais + 1
    fim
  fim
fim

desenhe()

  potigol_img.desenhe(484, 6, 1, 0, 0.4, 0.4)

  fonte16.desenhe("POTIGOL GAMES", 492, 160, 0, Cor.PRETO)

  #código para desenhar o jogo
  #desenhando o tabuleiro
  tabuleiro_8x8.desenhe(0, 0, 0, 0, 0.3, 0.3)

  para i de 1 até 8,
       j de 1 até 8 faça
    se ocupado[i][j] então
      rainha_branca.desenhe(d_x * i - 30, d_y * j - 30, 1, 0, 0.08, 0.08)
    fim
  fim

  #peças
  fonte12.desenhe("Rainhas Disponiveis", 500, 190, 0, Cor.PRETO)

  var p := 0
  para i de 1 até 2,
       j de 1 até 4 faça

    se p < pecas_originais então
      rainha_branca.desenhe(496 + d_x / 2 * j - 30, 180 + d_y * i - 30, 1, 0, 0.08, 0.08)
    fim
    p := p + 1
  fim

  #botões
  #...
  fonte12.desenhe("Estado do Botão: {botao(0, 0, 100, 100)} ", 500, 400, 0, Cor.PRETO)

  marcador_img.desenhe(marcador.x, marcador.y, 1, 0, 0.08, 0.08)
fim

jogo.iniciar("OITO RAINHAS", 640, 480, atualize, desenhe)
